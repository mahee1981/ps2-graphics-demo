.init_vf_all
.init_vi_all
.syntax new
.vu
--enter
--endenter
setup:
    lq matrixMvp[0], 0(vi00)           
    lq matrixMvp[1], 1(vi00)           
    lq matrixMvp[2], 2(vi00)           
    lq matrixMvp[3], 3(vi00)           
    xtop    iBase
    lq.xyz  viewPortTransform,  0(iBase)        
    lq      primTag,        1(iBase)            
    lq      rgba,           2(iBase)             
    iaddiu  vertexData,     iBase,      3           
    ilw.w   vertCount,      0(iBase)                
    iadd    stqData,        vertexData, vertCount   
    iadd    gifPacketStart, stqData,    vertCount   
    iadd    destAddress,    stqData,    vertCount   
    sqi primTag,    (destAddress++) 
	fcset   0x000000                            
    iadd vertexCounter, iBase, vertCount 
loop:
    lq vertex, 0(vertexData)                      
    lq Stq,  0(stqData)                         

    mul            acc,           matrixMvp[0], vertex[x]
    madd           acc,           matrixMvp[1], vertex[y]
    madd           acc,           matrixMvp[2], vertex[z]
    madd           vertex, matrixMvp[3], vertex[w]

    clipw.xyz	vertex, vertex			        
    fcand		VI01, 0x3FFFF                   
    iaddiu		iADC, VI01, 0x7FFF              
    isw.w		iADC,   2(destAddress)
    div         q,      vf00[w],    vertex[w]   
    mul.xyz     vertex, vertex,     q
    mula.xyz    acc,    viewPortTransform,      vf00[w]     
    madd.xyz    vertex, vertex,     viewPortTransform       
    ftoi4.xyz   vertex, vertex                  
    sq Stq,      0(destAddress)         
    sq rgba,        1(destAddress)      
    sq.xyz vertex,  2(destAddress)      
    iaddiu    vertexData,  vertexData, 1   
    iaddiu    stqData, stqData,  1          
    iaddiu    destAddress, destAddress, 3 
    iaddi   vertexCounter,  vertexCounter,  -1	
    ibne    vertexCounter,  iBase,   loop	
loopend:
    --barrier
    xgkick gifPacketStart 
--exit
--endexit
